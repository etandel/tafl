WHITESPACE = _{" "}

goodbye_cmd = {"goodbye"}

finish_code = {'0'..'3'}
finish_cmd = {"finish" ~ finish_code}

error_code = {'1'..'4'}
error_cmd = {"error" ~ error_code}

side = {"attackers" | "defenders"}

play_cmd = {"play" ~ side}

side_cmd = {"side" ~ side}

int = @{ASCII_DIGIT+}
ot_marker = _{"*"}
clock_cmd = {"clock" ~ int ~ ot_marker? ~ int ~ ot_marker? ~ int ~ int ~ int}

analyze_cmd = {"analyze" ~ int ~ int}

row_sep = _{"/"}
piece = {"m" | "M" | "K" | "n" | "N" | "c" | "C" | "t" | "T"}
row_record = {(piece | int)+}
position = {row_sep ~ (row_record ~ row_sep)+}

position_cmd = {"position" ~ position}

move_cmd = {"move" ~ position}

square = @{ASCII_ALPHA_LOWER+ ~ ASCII_DIGIT+}
move_sep = _{"|"}
// only simple notation is supported
move_ = {square ~ "-" ~ square}
opponent_move_cmd = {
    "opponent-move" ~ move_ ~ (move_sep ~ move_)* ~ position
}

cmd = {
    goodbye_cmd
    | finish_cmd
    | error_cmd
    | side_cmd
    | play_cmd
    | clock_cmd
    | analyze_cmd
    | position_cmd
    | move_cmd
    | opponent_move_cmd
}
